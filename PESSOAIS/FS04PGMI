      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         FS04PGMI.
       AUTHOR.                             GABRIEL

      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
       77  WS-MSG-ERRO                     PIC X(80).
       77  WS-LENGTH                       PIC S9(04) COMP.

       01  WS-VAR-TEMPO.
           05 WS-DATA                      PIC X(10).
           05 WS-HORARIO                   PIC X(08).

       01  WS-DFHCOMMAREA.
           05 WS-FASE                      PIC X(01).
      *BACKUP DO REGISTRO
           05  WS-BKP-DCLTBPRODUTO         PIC X(73).

      *MAPA REFERENTE A TELA DE CADASTRO
           COPY FS04CAD.
      *COMANDO TECLAS PRESSIONADAS
           COPY DFHAID.
      *CARACTERES E ATRIBUTOS
           COPY DFHBMSCA.
      *VARIAVEL ESPELHO DO SQLD
           EXEC SQL
           INCLUDE BK002TP
           END-EXEC.

           EXEC SQL
           INCLUDE SQLCA
           END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
       01  DFHCOMMAREA.
           05 OCCURS 0 TO 24579 TIMES DEPENDING ON EIBCALEN
                                           PIC X(01).
      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
      *ROTINA DEFINIDA PARA LIDAR COM ERROS DE MAPA E GENERICOS--------*
           EXEC CICS HANDLE CONDITION
              MAPFAIL(999-MAPFAIL)
              ERROR(999-ERROR)
           END-EXEC

      * SELETOR DE FASE - O MENU PRINCIPAL POSSUI 4 FASES
      *    FASE 1 - ENVIA O MAPA PARA O TERMINAL
      *    FASE 2 - TRATA O CAMPO T2COD
      *    FASE 3 - TRATA OS DEMAIS CAMPOS DO PRODUTO
      *    FASE 4 - TRATA O CAMPO T2CONF
      *
           MOVE DFHCOMMAREA                TO WS-DFHCOMMAREA

           IF EIBCALEN EQUAL 0
              MOVE +80                     TO WS-LENGTH
              MOVE 'UTILIZE A OPCAO B DA TRANSACAO F04M'
                                           TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-IF

           EVALUATE WS-FASE
              WHEN '1' PERFORM 100-FASE1
              WHEN '2' PERFORM 200-FASE2
              WHEN '3' PERFORM 300-FASE3
              WHEN '4' PERFORM 400-FASE4
              WHEN OTHER
                 MOVE +80                  TO WS-LENGTH
                 MOVE 'ERRO NO NUMERO DA FASE'
                                           TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-EVALUATE
           .

       100-FASE1.
           MOVE LOW-VALUES                 TO MAPACADO
           MOVE -1                         TO T2CODL
           MOVE 'ENTRE O CODIGO DO PRODUTO PARA ALTERAR'
                                           TO T2MSGO
           MOVE 'ALTERACAO - DB2'     TO T2TELAO
           PERFORM 999-PROTECAO-FASE2
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .

       200-FASE2.
           EXEC CICS HANDLE AID
              ENTER   (210-ENTER)
              PF3     (220-PF3)
              PF5     (230-PF5)
              CLEAR   (230-PF5)
              ANYKEY  (240-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPACAD')
              MAPSET('FS04CAD')
              INTO  (MAPACADI)
           END-EXEC
           .

       210-ENTER.
           IF T2CODL EQUAL 0 OR T2CODI EQUAL SPACES
              MOVE 'CODIGO NAO INFORMADO - TENTE NOVAMENTE'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE2
           END-IF
           
           MOVE T2CODI                 TO CODPROD
           EXEC SQL
              SELECT CODPROD
                    , DESCPROD
                    , UNIDPROD
                    , LOCALPROD
                    , QTDEST
                    , QTDMAX
                    , QTDMIN
                    , PRECOCOMPRA
                    , PRECOVENDA
                    , PERCOMIS
              INTO   :CODPROD
                   , :DESCPROD
                   , :UNIDPROD
                   , :LOCALPROD
                   , :QTDEST
                   , :QTDMAX
                   , :QTDMIN
                   , :PRECOCOMPRA
                   , :PRECOVENDA
                   , :PERCOMIS
              FROM TBPRODUTO
              WHERE CODPROD = :CODPROD
           END-EXEC

           IF SQLCODE = +100
              PERFORM 999-NOTFND
           ELSE IF SQLCODE NOT = 0
              PERFORM 999-ERROR
              END-IF
           END-IF

           MOVE DCLTBPRODUTO               TO WS-BKP-DCLTBPRODUTO

           MOVE CODPROD             TO T2CODO
           MOVE DESCPROD            TO T2DESCO
           MOVE UNIDPROD            TO T2UNIDO
           MOVE LOCALPROD           TO T2LOCALO
           MOVE QTDEST              TO T2ESTO
           MOVE QTDMAX              TO T2MAXO
           MOVE QTDMIN              TO T2MINO
           MOVE PRECOCOMPRA         TO T2COMPO
           MOVE PRECOVENDA          TO T2VENDO
           MOVE PERCOMIS            TO T2COMISO

           MOVE 'DIGITE AS ALTERACOES NECESSARIAS'
                                           TO T2MSGO
           PERFORM 999-TRATA-FASE3
           .

       220-PF3.
           MOVE '1'                       TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('FS04PGMM')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       230-PF5.
           PERFORM 999-CHAMA-FASE1
           .

       240-ANYKEY.
           MOVE 'TECLA PRESSIONADA INVALIDA!'
                                           TO T2MSGO
           PERFORM 999-TRATA-FASE2
           .

       300-FASE3.
           EXEC CICS HANDLE AID
              ENTER   (310-ENTER)
              PF3     (220-PF3)
              PF5     (230-PF5)
              CLEAR   (230-PF5)
              ANYKEY  (240-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPACAD')
              MAPSET('FS04CAD')
              INTO  (MAPACADI)
           END-EXEC
           .

       310-ENTER.
      * LIMPAR OS CAMPOS NUMERICOS DE CARACTERES ALFANUMERICOS
           EXEC CICS BIF DEEDIT FIELD(T2ESTI   ) END-EXEC
           EXEC CICS BIF DEEDIT FIELD(T2MAXI   ) END-EXEC
           EXEC CICS BIF DEEDIT FIELD(T2MINI   ) END-EXEC
           EXEC CICS BIF DEEDIT FIELD(T2COMPI  ) END-EXEC
           EXEC CICS BIF DEEDIT FIELD(T2VENDI  ) END-EXEC
           EXEC CICS BIF DEEDIT FIELD(T2COMISI ) END-EXEC

      * VERIFICAR SE HA ALGUM CAMPO NAO DIGITADO
           IF T2DESCL  = 0 OR T2DESCI EQUAL SPACES OR
              T2UNIDL  = 0 OR T2UNIDI EQUAL SPACES OR
              T2LOCALL = 0 OR T2UNIDI EQUAL SPACES OR
              T2ESTL   = 0 OR
              T2MAXL   = 0 OR
              T2MINL   = 0 OR
              T2COMPL  = 0 OR
              T2VENDL  = 0 OR
              T2COMISL = 0

              MOVE 'FAVOR PREENCHER TODOS OS CAMPOS'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE3
           END-IF

      * VERIFICAR SE MAX > MIN
           IF T2MAXI <= T2MINI
              MOVE 'QTD. MAX. DEVE SER MAIOR QUE QTD. MIN.'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE3
           END-IF
      * TRATA FASE4
           PERFORM 999-TRATA-FASE4
           .

       999-PROTECAO-FASE2.
      * DESPROTEGER
           MOVE DFHUNIMD                      TO T2CODA

      * PROTEGER
           MOVE 'Z'                           TO T2DESCA
           MOVE 'Z'                           TO T2UNIDA
           MOVE 'Z'                           TO T2LOCALA
           MOVE 'Z'                           TO T2ESTA
           MOVE 'Z'                           TO T2MAXA
           MOVE 'Z'                           TO T2MINA
           MOVE 'Z'                           TO T2COMPA
           MOVE 'Z'                           TO T2VENDA
           MOVE 'Z'                           TO T2COMISA
           MOVE 'Z'                           TO T2CONFA

      *UNDERLINE
           MOVE DFHUNDLN                      TO T2CODH

      * DESPROTEGER
      *    MOVE DFHALL                        TO T2CONFH
           .

       400-FASE4.
           EXEC CICS HANDLE AID
              ENTER   (410-ENTER)
              PF3     (220-PF3)
              PF5     (230-PF5)
              CLEAR   (230-PF5)
              ANYKEY  (240-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPACAD')
              MAPSET('FS04CAD')
              INTO  (MAPACADI)
           END-EXEC
           .

       410-ENTER.
           IF T2CONFL EQUAL 0
      *        OR T2CONFI NOT EQUAL 'S' AND 'N'

              MOVE 'DIGITE UM S=SIM OU N=NAO'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE4
           END-IF

           IF  T2CONFI = 'S'
      *        ALTERAR O REGISTRO

               IF DCLTBPRODUTO NOT = WS-BKP-DCLTBPRODUTO
                  MOVE LOW-VALUES          TO MAPACADO
      *        PRODUTO FOI ALTERADO POR OUTRO USUARIO
                  STRING 'O PRODUTO ' DELIMITED BY SIZE
                  CODPROD DELIMITED BY SPACE
                  ' FOI ALTERADO POR OUTRO USUARIO'
                                      DELIMITED BY SIZE
                                           INTO T2MSGO
                  PERFORM 999-TRATA-FASE2
               END-IF

               MOVE T2CODI                 TO CODPROD
               MOVE T2DESCI                TO DESCPROD
               MOVE T2UNIDI                TO UNIDPROD
               MOVE T2LOCALI               TO LOCALPROD
               MOVE T2ESTI                 TO QTDEST
               MOVE T2MAXI                 TO QTDMAX
               MOVE T2MINI                 TO QTDMIN
               MOVE T2COMPI                TO PRECOCOMPRA
               MOVE T2VENDI                TO PRECOVENDA
               MOVE T2COMISI               TO PERCOMIS

                  EXEC SQL
                     UPDATE TBPRODUTO
                       SET CODPROD = :CODPROD,
                           DESCPROD = :DESCPROD,
                           UNIDPROD = :UNIDPROD,
                           LOCALPROD = :LOCALPROD,
                           QTDEST = :QTDEST,
                           QTDMAX = :QTDMAX,
                           QTDMIN = :QTDMIN,
                           PRECOCOMPRA = :PRECOCOMPRA,
                           PRECOVENDA = :PRECOVENDA,
                           PERCOMIS = :PERCOMIS
                       WHERE CODPROD = :CODPROD
                  END-EXEC
               EVALUATE SQLCODE
                  WHEN +100
                     STRING 'O PRODUTO '       DELIMITED BY SIZE
                     CODPROD DELIMITED BY SPACE
                     ' FOI ALTERADO POR OUTRO USUARIO'
                                      DELIMITED BY SIZE
                                           INTO T2MSGO
                        PERFORM 999-NOTFND
                  WHEN -545
                     STRING 'O PRODUTO '       DELIMITED BY SIZE
                     CODPROD DELIMITED BY SPACE
                     ' APRESENTA ERROS DE INTEGRIDADE'
                                      DELIMITED BY SIZE
                                           INTO T2MSGO
                        PERFORM 999-ERROR
                  WHEN 0
                     STRING 'O PRODUTO '       DELIMITED BY SIZE
                     CODPROD DELIMITED BY SPACE
                     ' FOI ALTERADO COM SUCESSO'
                                               DELIMITED BY SIZE
                                           INTO T2MSGO
               END-EVALUATE
           ELSE
              IF T2CONFI = 'N'
              PERFORM 100-FASE1
              END-IF
           END-IF

           PERFORM 220-PF3
           .
       999-TRATA-FASE3.
           MOVE -1                         TO T2DESCL

           PERFORM 999-PROTECAO-FASE3
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE3
           .

       999-ENCERRA-TRANSACAO.
           EXEC CICS SEND TEXT
              FROM (WS-MSG-ERRO)
              LENGTH(WS-LENGTH)
              ERASE FREEKB ALARM
           END-EXEC

           EXEC CICS RETURN
           END-EXEC
           .

       999-MANDA-TELA.
           MOVE EIBTRMID                  TO T2TERMO
           MOVE EIBTRNID                  TO T2TRANO
           MOVE EIBTASKN                  TO T2TASKO
           MOVE WS-FASE                   TO T2FASEO

           EXEC CICS ASSIGN
              USERID(T2USERO)
           END-EXEC

           EXEC CICS LINK
               PROGRAM('AUXCICS1')
               COMMAREA(WS-VAR-TEMPO)
               LENGTH(+18)
           END-EXEC

           MOVE WS-DATA                   TO T2DATAO
           MOVE WS-HORARIO                TO T2HORAO

           EXEC CICS SEND
              MAP ('MAPACAD')
              MAPSET('FS04CAD')
              FROM(MAPACADO)
              ERASE FREEKB ALARM CURSOR
           END-EXEC
           .

       999-CHAMA-FASE1.
           MOVE '1'                       TO WS-FASE

           EXEC CICS XCTL
              PROGRAM('FS04PGMD')
              COMMAREA(WS-DFHCOMMAREA)
              LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-CHAMA-FASE2.
           MOVE '2'                       TO WS-FASE

           EXEC CICS RETURN
               TRANSID('F04D')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-CHAMA-FASE3.
           MOVE '3'                       TO WS-FASE

           EXEC CICS RETURN
               TRANSID('F04D')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-CHAMA-FASE4.
           MOVE '4'                       TO WS-FASE

           EXEC CICS RETURN
               TRANSID('F04D')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-TRATA-FASE2.
           MOVE -1                        TO T2CODL

           PERFORM 999-PROTECAO-FASE2
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .

       999-PROTECAO-FASE3.
           MOVE -1                         TO T2DESCL
      * DESPROTEGER OS CAMPOS DE REGISTRO

           MOVE DFHUNIMD                   TO T2DESCA
           MOVE DFHUNIMD                   TO T2UNIDA
           MOVE DFHUNIMD                   TO T2LOCALA
           MOVE DFHUNIMD                   TO T2ESTA
           MOVE DFHUNIMD                   TO T2MAXA
           MOVE DFHUNIMD                   TO T2MINA
           MOVE DFHUNIMD                   TO T2COMPA
           MOVE DFHUNIMD                   TO T2VENDA
           MOVE DFHUNIMD                   TO T2COMISA

      * UNDERLINE
           MOVE DFHUNDLN                   TO T2DESCH
           MOVE DFHUNDLN                   TO T2UNIDH
           MOVE DFHUNDLN                   TO T2LOCALH
           MOVE DFHUNDLN                   TO T2ESTH
           MOVE DFHUNDLN                   TO T2MAXH
           MOVE DFHUNDLN                   TO T2MINH
           MOVE DFHUNDLN                   TO T2COMPH
           MOVE DFHUNDLN                   TO T2VENDH
           MOVE DFHUNDLN                   TO T2COMISH

      * PROTEGER E TIRAR UNDELINE
           MOVE DFHBMPRF                   TO T2CODA
           MOVE DFHBMPRF                   TO T2CONFA
           MOVE DFHALL                     TO T2CODH
           .

       999-PROTECAO-FASE4.
           MOVE -1                         TO T2CONFL
      * PROTEGER OS CAMPOS DE REGISTRO

           MOVE DFHBMPRF                   TO T2CODA
           MOVE DFHBMPRF                   TO T2DESCA
           MOVE DFHBMPRF                   TO T2UNIDA
           MOVE DFHBMPRF                   TO T2LOCALA
           MOVE DFHBMPRF                   TO T2ESTA
           MOVE DFHBMPRF                   TO T2MAXA
           MOVE DFHBMPRF                   TO T2MINA
           MOVE DFHBMPRF                   TO T2COMPA
           MOVE DFHBMPRF                   TO T2VENDA
           MOVE DFHBMPRF                   TO T2COMISA

      * UNDERLINE
           MOVE DFHALL                    TO T2DESCH
           MOVE DFHALL                    TO T2UNIDH
           MOVE DFHALL                    TO T2LOCALH
           MOVE DFHALL                    TO T2ESTH
           MOVE DFHALL                    TO T2MAXH
           MOVE DFHALL                    TO T2MINH
           MOVE DFHALL                    TO T2COMPH
           MOVE DFHALL                    TO T2VENDH
           MOVE DFHALL                    TO T2COMISH

      * DESPROTEGER CONFIRMACAO E UNDELINE
           MOVE DFHUNIMD                   TO T2CONFA
           MOVE DFHUNDLN                   TO T2CONFH
           .

       999-TRATA-FASE4.
           MOVE -1                         TO T2CONFL
           MOVE 'CONFIRMA ALTERACAO(S/N)?' TO T2ACAOO

           PERFORM 999-PROTECAO-FASE4
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE4
           .

       999-MAPFAIL.
           MOVE 'ERRO MAPA FS04CAD'        TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .

       999-ERROR.
           MOVE 'ERRO GENERICO FS04PGMD'   TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
       999-NOTFND.
           MOVE T2CODI                     TO CODPROD
           MOVE LOW-VALUES                 TO MAPACADO

           IF WS-FASE = 2
              STRING 'O PRODUTO '          DELIMITED BY SIZE
              CODPROD                      DELIMITED BY SPACES
                     'NAO FOI LOCALIZADO'  DELIMITED BY SIZE
                                           INTO T2MSGO
           ELSE
              STRING 'O PRODUTO'           DELIMITED BY SIZE
              CODPROD                      DELIMITED BY SPACES
                     ' FOI EXCLUIDO POR OUTRO USUARIO'
                                           DELIMITED BY SIZE
                                           INTO T2MSGO
           END-IF
           PERFORM 999-TRATA-FASE2
           .
