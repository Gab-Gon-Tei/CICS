      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         FS04PGMF.
       AUTHOR.                             GABRIEL

      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
       77  WS-MSG-ERRO                     PIC X(80).
       77  WS-LENGTH                       PIC S9(04) COMP.

       01  WS-VAR-TEMPO.
           05 WS-DATA                      PIC X(10).
           05 WS-HORARIO                   PIC X(08).

       01  WS-DFHCOMMAREA.
           05 WS-FASE                      PIC X(01).

      *MAPA REFERENTE A TELA DE CADASTRO
           COPY FS04CAD.
      *COMANDO TECLAS PRESSIONADAS
           COPY DFHAID.
      *CARACTERES E ATRIBUTOS
           COPY DFHBMSCA.

           EXEC SQL
              INCLUDE BK002TP
           END-EXEC.

           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
       01  DFHCOMMAREA.
           05 OCCURS 0 TO 24579 TIMES DEPENDING ON EIBCALEN
                                           PIC X(01).
      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
      *ROTINA DEFINIDA PARA LIDAR COM ERROS DE MAPA E GENERICOS--------*
           EXEC CICS HANDLE CONDITION
              MAPFAIL(999-MAPFAIL)
              ERROR(999-ERROR)
           END-EXEC

      * SELETOR DE FASE - O MENU PRINCIPAL POSSUI 3 FASES
      *    FASE 1 - ENVIA O MAPA PARA O TERMINAL
      *    FASE 2 - TRATA O CAMPO T2COD
      *    FASE 3 - TRATA O CAMPO T2CONF
      *
           MOVE DFHCOMMAREA                TO WS-DFHCOMMAREA

           IF EIBCALEN EQUAL 0
              MOVE +80                     TO WS-LENGTH
              MOVE 'UTILIZE A OPCAO A DA TRANSACAO F04M'
                                           TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-IF

           EVALUATE WS-FASE
              WHEN '1' PERFORM 100-FASE1
              WHEN '2' PERFORM 200-FASE2
              WHEN '3' PERFORM 300-FASE3
              WHEN OTHER
                 MOVE +80                  TO WS-LENGTH
                 MOVE 'ERRO NO NUMERO DA FASE'
                                           TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-EVALUATE
           .

       100-FASE1.
           MOVE LOW-VALUES                 TO MAPACADO
           MOVE -1                         TO T2CODL
           MOVE 'ENTRE O CODIGO DO PRODUTO PARA CONSULTAR'
                                           TO T2MSGO
           MOVE 'CONSULTA - DB2'           TO T2TELAO
           PERFORM 999-PROTECAO-FASE2
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .

       200-FASE2.
           EXEC CICS HANDLE AID
              ENTER   (210-ENTER)
              PF3     (220-PF3)
              PF5     (230-PF5)
              CLEAR   (230-PF5)
              ANYKEY  (240-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPACAD')
              MAPSET('FS04CAD')
              INTO  (MAPACADI)
           END-EXEC
           .

       210-ENTER.
           IF T2CODL EQUAL 0 OR T2CODI EQUAL SPACES
              MOVE 'CODIGO NAO INFORMADO - TENTE NOVAMENTE'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE2
           END-IF

           MOVE T2CODI                     TO CODPROD

           EXEC SQL
              SELECT CODPROD
                    ,DESCPROD
                    ,UNIDPROD
                    ,LOCALPROD
                    ,QTDEST
                    ,QTDMAX
                    ,QTDMIN
                    ,PRECOCOMPRA
                    ,PRECOVENDA
                    ,PERCOMIS
              INTO :CODPROD
                  ,:DESCPROD
                  ,:UNIDPROD
                  ,:LOCALPROD
                  ,:QTDEST
                  ,:QTDMAX
                  ,:QTDMIN
                  ,:PRECOCOMPRA
                  ,:PRECOVENDA
                  ,:PERCOMIS
              FROM TBPRODUTO
              WHERE CODPROD = :CODPROD
           END-EXEC

           IF SQLCODE = +100
              PERFORM 999-NOTFND
           END-IF
      * MOVER PARA DA HOSPEDEIRA PARA A TELA
           MOVE CODPROD                    TO T2CODO
           MOVE DESCPROD-TEXT              TO T2DESCO
           MOVE UNIDPROD                   TO T2UNIDO
           MOVE LOCALPROD                  TO T2LOCALO
           MOVE QTDEST                     TO T2ESTO
           MOVE QTDMAX                     TO T2MAXO
           MOVE QTDMIN                     TO T2MINO
           MOVE PRECOCOMPRA                TO T2COMPO
           MOVE PRECOVENDA                 TO T2VENDO
           MOVE PERCOMIS                   TO T2COMISO

           PERFORM 999-TRATA-FASE3
           .

       220-PF3.
           MOVE '1'                       TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('FS04PGMM')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       230-PF5.
           PERFORM 999-CHAMA-FASE1
           .

       240-ANYKEY.
           MOVE 'TECLA PRESSIONADA INVALIDA!'
                                           TO T2MSGO
           PERFORM 999-TRATA-FASE2
           .

       300-FASE3.
           EXEC CICS HANDLE AID
              ENTER   (310-ENTER)
              PF3     (220-PF3)
              PF5     (230-PF5)
              CLEAR   (230-PF5)
              ANYKEY  (240-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPACAD')
              MAPSET('FS04CAD')
              INTO  (MAPACADI)
           END-EXEC
           .

       310-ENTER.
           IF T2CONFL EQUAL 0 OR
              T2CONFI NOT EQUAL 'S' AND 'N'
              MOVE 'DIGITE S=SIM OU N=NAO'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE3
           END-IF

           IF T2CONFI = 'S'
              PERFORM 100-FASE1
           END-IF
           PERFORM 220-PF3
           .

       999-PROTECAO-FASE2.
      * DESPROTEGER
           MOVE DFHUNIMD                      TO T2CODA

      * PROTEGER
           MOVE 'Z'                           TO T2DESCA
           MOVE 'Z'                           TO T2UNIDA
           MOVE 'Z'                           TO T2LOCALA
           MOVE 'Z'                           TO T2ESTA
           MOVE 'Z'                           TO T2MAXA
           MOVE 'Z'                           TO T2MINA
           MOVE 'Z'                           TO T2COMPA
           MOVE 'Z'                           TO T2VENDA
           MOVE 'Z'                           TO T2COMISA
           MOVE 'Z'                           TO T2CONFA

      *UNDERLINE
           MOVE DFHUNDLN                      TO T2CODH

      * DESPROTEGER
           MOVE DFHALL                        TO T2CONFH
           .

       999-PROTECAO-FASE3.

      * PROTEGER
           MOVE 'Z'                           TO T2DESCA
           MOVE 'Z'                           TO T2UNIDA
           MOVE 'Z'                           TO T2LOCALA
           MOVE 'Z'                           TO T2ESTA
           MOVE 'Z'                           TO T2MAXA
           MOVE 'Z'                           TO T2MINA
           MOVE 'Z'                           TO T2COMPA
           MOVE 'Z'                           TO T2VENDA
           MOVE 'Z'                           TO T2COMISA
           MOVE 'Z'                           TO T2CODA
      *    MOVE DFHALL                        TO T2CODA
           .
       999-ENCERRA-TRANSACAO.
           EXEC CICS SEND TEXT
              FROM (WS-MSG-ERRO)
              LENGTH(WS-LENGTH)
              ERASE FREEKB ALARM
           END-EXEC

           EXEC CICS RETURN
           END-EXEC
           .

       999-MANDA-TELA.
           MOVE EIBTRMID                  TO T2TERMO
           MOVE EIBTRNID                  TO T2TRANO
           MOVE EIBTASKN                  TO T2TASKO
           MOVE WS-FASE                   TO T2FASEO

           EXEC CICS ASSIGN
              USERID(T2USERO)
           END-EXEC

           EXEC CICS LINK
               PROGRAM('AUXCICS1')
               COMMAREA(WS-VAR-TEMPO)
               LENGTH(+18)
           END-EXEC

           MOVE WS-DATA                   TO T2DATAO
           MOVE WS-HORARIO                TO T2HORAO

           EXEC CICS SEND
              MAP ('MAPACAD')
              MAPSET('FS04CAD')
              FROM(MAPACADO)
              ERASE FREEKB ALARM CURSOR
           END-EXEC
           .

       999-CHAMA-FASE1.
           MOVE '1'                       TO WS-FASE

           EXEC CICS XCTL
              PROGRAM('FS04PGMF')
              COMMAREA(WS-DFHCOMMAREA)
              LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-CHAMA-FASE2.
           MOVE '2'                       TO WS-FASE

           EXEC CICS RETURN
               TRANSID('F04F')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-CHAMA-FASE3.
           MOVE '3'                       TO WS-FASE

           EXEC CICS RETURN
               TRANSID('F04F')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-TRATA-FASE2.
           MOVE -1                        TO T2CODL

           PERFORM 999-PROTECAO-FASE2
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .

       999-TRATA-FASE3.
           MOVE -1                        TO T2CONFL
           MOVE 'CONSULTAR OUTRO (S/N)?'  TO T2ACAOO
      * MOVE OS CAMPOS DO REGISTRO PARA A TELA

           MOVE CODPROD                   TO T2CODO
           MOVE DESCPROD-TEXT             TO T2DESCO
           MOVE UNIDPROD                  TO T2UNIDO
           MOVE LOCALPROD                 TO T2LOCALO
           MOVE QTDEST                    TO T2ESTO
           MOVE QTDMAX                    TO T2MAXO
           MOVE QTDMIN                    TO T2MINO
           MOVE PRECOCOMPRA               TO T2COMPO
           MOVE PRECOVENDA                TO T2VENDO
           MOVE PERCOMIS                  TO T2COMISO

           PERFORM 999-PROTECAO-FASE3
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE3
           .

       999-MAPFAIL.
           MOVE 'ERRO MAPA FS04CAD'        TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .

       999-ERROR.
           MOVE 'ERRO GENERICO FS04PGMM'   TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
       999-NOTFND.
           STRING 'PRODUTO '               DELIMITED BY SIZE
                     CODPROD               DELIMITED BY SPACE
                  ' NAO LOCALIZADO'        DELIMITED BY SIZE
                                           INTO T2MSGO
           PERFORM 999-TRATA-FASE2
           .
