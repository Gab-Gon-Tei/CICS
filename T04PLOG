      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         T04PLOG.
       AUTHOR.                             GABRIEL

      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
       77  WS-MSG-ERRO                     PIC X(80).
       77  WS-LENGTH                       PIC S9(04) COMP.

       01  WS-DATA.
           05 WS-ANO                       PIC X(02).
           05 WS-MES                       PIC X(02).
           05 WS-DIA                       PIC X(02).

       01  WS-HORARIO.
           05 WS-HORA                      PIC X(02).
           05 WS-MIN                       PIC X(02).
           05 WS-SEG                       PIC X(02).

       01  WS-DATA-F.
           05 WS-DIA-F                     PIC X(02).
           05 FILLER                       PIC X(01) VALUE '/'.
           05 WS-MES-F                     PIC X(02).
           05 FILLER                       PIC X(01) VALUE '/'.
           05 WS-ANO-F                     PIC X(02).

       01  WS-HORARIO-F.
           05 WS-HORA-F                    PIC X(02).
           05 FILLER                       PIC X(01) VALUE ':'.
           05 WS-MIN-F                     PIC X(02).
           05 FILLER                       PIC X(01) VALUE ':'.
           05 WS-SEG-F                     PIC X(02).

       01  WS-DFHCOMMAREA.
           05 WS-FASE                      PIC X(01).
           05 WS-ID-CPF                    PIC X(11).


      *MAPA REFERENTE A TELA DE CADASTRO
           COPY T04MLOG.
      *COMANDO TECLAS PRESSIONADAS
           COPY DFHAID.
      *CARACTERES E ATRIBUTOS
           COPY DFHBMSCA.

           EXEC SQL
              INCLUDE BKCLIT
           END-EXEC.

           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
       01  DFHCOMMAREA.
           05 OCCURS 0 TO 24579 TIMES DEPENDING ON EIBCALEN
                                           PIC X(01).
      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
      *ROTINA DEFINIDA PARA LIDAR COM ERROS DE MAPA E GENERICOS--------*
           EXEC CICS HANDLE CONDITION
              MAPFAIL(999-MAPFAIL)
              ERROR(999-ERROR)
           END-EXEC

      * SELETOR DE FASE - O MENU PRINCIPAL POSSUI 3 FASES
      *    FASE 1 - ENVIA O MAPA PARA O TERMINAL
      *    FASE 2 - TRATA O CAMPO T1USER
      *
           MOVE DFHCOMMAREA                TO WS-DFHCOMMAREA

           IF EIBCALEN EQUAL 0
              MOVE '1'                     TO WS-FASE
           END-IF

           EVALUATE WS-FASE
              WHEN '1'  PERFORM 100-FASE1
              WHEN '2' PERFORM 200-FASE2
              WHEN OTHER
                 MOVE +80                  TO WS-LENGTH
                 MOVE 'ERRO NO NUMERO DA FASE'
                                           TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-EVALUATE
           .

       100-FASE1.
           MOVE LOW-VALUES                 TO MAPLOGO
           MOVE -1                         TO T1USERL
           MOVE 'FACA LOGIN OU CADASTRE-SE' TO T1MSGO
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .

       200-FASE2.
           EXEC CICS HANDLE AID
              ENTER   (210-ENTER)
              PF3     (220-PF3)
              PF5     (230-PF5)
              CLEAR   (230-PF5)
              PF2     (240-PF2)
              ANYKEY  (250-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPLOG')
              MAPSET('T04MLOG')
              INTO  (MAPLOGI)
           END-EXEC
           .

       210-ENTER.
           IF T1USERL < 5 OR T1USERI EQUAL SPACES OR
              T1SENHAL < 5 OR T1SENHAI EQUAL SPACES
              MOVE 'PREENCHA SEU USUARIO E SENHA'
                                           TO T1MSGO
              PERFORM 999-TRATA-FASE2
           END-IF

           MOVE T1USERI                    TO DCLCLI-NOME-USUARIO
           PERFORM 210-VALIDA-USUARIO
           PERFORM 210-VALIDA-SENHA
           MOVE DCLCLI-CPF                 TO WS-ID-CPF
           PERFORM 260-LISTAGEM-DE-PRODUTOS
           .

       210-VALIDA-USUARIO.
           EXEC SQL
              SELECT NOME_USUARIO
                    ,SENHA
                    ,CPF
              INTO :DCLCLI-NOME-USUARIO
                   ,:DCLCLI-SENHA
                   ,:DCLCLI-CPF
              FROM CLIENTES
              WHERE NOME_USUARIO = :DCLCLI-NOME-USUARIO
           END-EXEC

           IF SQLCODE = +100
               MOVE 'USUARIO NAO ENCONTRADO'
                                           TO T1MSGO
               PERFORM 999-TRATA-FASE2
           ELSE
               IF SQLCODE NOT EQUAL 0
                   MOVE 'ERRO AO CONSULTAR USUARIO'
                                           TO T1MSGO
                   PERFORM 999-TRATA-FASE2
               END-IF
           END-IF
           .

       210-VALIDA-SENHA.
           IF DCLCLI-SENHA EQUAL T1SENHAI
               CONTINUE
           ELSE
               MOVE 'SENHA INCORRETA'         TO T1MSGO
               PERFORM 999-TRATA-FASE2
           END-IF
           .

       260-LISTAGEM-DE-PRODUTOS.
           MOVE '1'                       TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('T04PPRL')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       220-PF3.
           MOVE +80                        TO WS-LENGTH
           MOVE 'FIM NORMAL DA TRANSACAO FT4A'
                                           TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .

       230-PF5.
           PERFORM 999-CHAMA-FASE1
           .

       240-PF2.
           MOVE '1'                       TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('T04PCAD')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       250-ANYKEY.
           MOVE 'TECLA PRESSIONADA INVALIDA!'
                                           TO T1MSGO
           PERFORM 999-TRATA-FASE2
           .

       999-ENCERRA-TRANSACAO.
           EXEC CICS SEND TEXT
              FROM (WS-MSG-ERRO)
              LENGTH(WS-LENGTH)
              ERASE FREEKB ALARM
           END-EXEC

           EXEC CICS RETURN
           END-EXEC
           .

       999-MANDA-TELA.
           MOVE EIBTRMID                  TO T1TERMO
           MOVE EIBTRNID                  TO T1TRANSO
           MOVE EIBTASKN                  TO T1TASKO
           MOVE WS-FASE                   TO T1FASEO

           ACCEPT WS-DATA FROM DATE
           ACCEPT WS-HORARIO FROM TIME

           MOVE WS-DIA                     TO WS-DIA-F
           MOVE WS-MES                     TO WS-MES-F
           MOVE WS-ANO                     TO WS-ANO-F

           MOVE WS-HORA                    TO WS-HORA-F
           MOVE WS-MIN                     TO WS-MIN-F
           MOVE WS-SEG                     TO WS-SEG-F

           MOVE WS-DATA-F                   TO T1DATAO
           MOVE WS-HORARIO-F                TO T1HORAO
      *    MOVE '11111'                   TO T1MSGO

           EXEC CICS SEND
              MAP ('MAPLOG')
              MAPSET('T04MLOG')
              FROM(MAPLOGO)
              ERASE FREEKB ALARM CURSOR
           END-EXEC
           .

       999-CHAMA-FASE1.
           MOVE '1'                       TO WS-FASE

           MOVE 'DIGITE USUARIO E SENHA PARA ENTRAR OU EFETUE CADASTRO'
                                          TO T1MSGO
           EXEC CICS XCTL
              PROGRAM('T04PLOG')
              COMMAREA(WS-DFHCOMMAREA)
              LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-CHAMA-FASE2.
           MOVE '2'                       TO WS-FASE

           EXEC CICS RETURN
               TRANSID('FT4A')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-TRATA-FASE2.
      *    MOVE LOW-VALUES                TO MAPLOGO
           MOVE -1                        TO T1USERL

           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .

       999-MAPFAIL.
           MOVE 'ERRO MAPA T04MLOG'        TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .

       999-ERROR.
           MOVE 'ERRO GENERICO'   TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
