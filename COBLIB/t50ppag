      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         T10PPAG.
       AUTHOR.                             PEDRO.
      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
       77  WS-MSG-ERRO                     PIC X(80).
       77  WS-LENGTH                       PIC S9(04) COMP.

       01  WS-DFHCOMMAREA.
           05 WS-FASE                      PIC X(01).
           05 WS-ID-CPF                    PIC X(11).
           05 WS-ID-PROD                   PIC 9(09).
           05 WS-ID-PEDIDO                 PIC 9(09).
           05 WS-CONT-CARR                 PIC S9(09).
           05 WS-ID-CONTA-BANCARIA         PIC S9(09).
           05 WS-TIPO                      PIC X(01).
           05 WS-NUMERO-CARTAO             PIC X(16).

       01  WS-VAR-TEMPO.
           05 WS-DATA                      PIC X(10).
           05 WS-HORARIO                   PIC X(08).

       77  WS-SQLCODE                      PIC +9(09).
       77  WS-ITEM                         PIC S9(04) COMP.
       77  WS-DE                           PIC S9(05)V99 COMP.
       77  WS-ATE                          PIC S9(05)V99 COMP.
       77  WS-RESP                         PIC S9(08) COMP.
       77  WS-RESTO                        PIC 9(01).
       77  WS-INDEX                        PIC 9(02).
       77  WS-ALL-SPACES                   PIC X(15) VALUE  SPACES.
       77  WS-MSG                          PIC X(60).
       77  WS-FS                           PIC X(02).
       01  WS-ENDFILE-AUX                  PIC X(01).
           88 ENDFILE-YES                          VALUE 'S'.
           88 ENDFILE-NO                           VALUE 'N'.

       77  WS-ATIVO-AUX                    PIC X(1).
       77  WS-TIPO-AUX                     PIC X(1).
           COPY T10MPAG.
           COPY DFHAID.
           COPY DFHBMSCA.
      *
           EXEC SQL
              INCLUDE TCDCLPDD
           END-EXEC.

      *
           EXEC SQL
              INCLUDE TCDCLCRT
           END-EXEC.

      *
           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
       01  DFHCOMMAREA.
           05 OCCURS 0 TO 24576 TIMES DEPENDING ON EIBCALEN
                                           PIC X(01).
      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
           EXEC CICS HANDLE CONDITION
              NOTFND(999-NOTFND)
              MAPFAIL(999-MAPFAIL)
              ERROR(999-ERRO-GENERICO)
           END-EXEC

           MOVE DFHCOMMAREA                TO WS-DFHCOMMAREA
      *
           IF EIBCALEN EQUAL 0
              MOVE '1'                     TO WS-FASE
              MOVE '11111111111'           TO WS-ID-CPF
              MOVE  62                     TO WS-ID-PROD
              MOVE  1                      TO WS-ID-PEDIDO
              MOVE  1                      TO WS-CONT-CARR
              MOVE  4                      TO WS-ID-CONTA-BANCARIA
           END-IF

           EVALUATE WS-FASE
              WHEN '1' PERFORM 100-FASE1
              WHEN '2' PERFORM 200-FASE2
              WHEN OTHER
                   MOVE +80                TO WS-LENGTH
                   MOVE 'ERRO NO NUMERO DA FASE'
                                           TO MENPAGO
                   PERFORM 999-ENCERRA-TRANSACAO
           END-EVALUATE
           .
       100-FASE1.
           MOVE LOW-VALUES                  TO MAPAPAGO
           MOVE -1                          TO DEBPAGL
           MOVE 'DIGITE A FORMA DE PAGAMENTO DESEJADA!'
                                           TO MENPAGO
           MOVE WS-ID-PEDIDO               TO DCLPDD-ID-PEDIDO
           EXEC SQL
              SELECT   VALOR_TOTAL
              INTO    :DCLPDD-VALOR-TOTAL
              FROM PEDIDOS
              WHERE ID_PEDIDO  = :DCLPDD-ID-PEDIDO
           END-EXEC
           IF SQLCODE  EQUAL 100
              MOVE 'PEDIDO NAO ENCONTRADO'
                                         TO MENPAGO
               MOVE SQLCODE            TO WS-SQLCODE
               PERFORM 999-ERRO-GENERICO
           ELSE
               IF SQLCODE NOT EQUAL 0
                  MOVE 'ERRO NA TABELA PEDIDO'
                                            TO MENPAGO
                  MOVE SQLCODE            TO WS-SQLCODE
                  PERFORM 999-ERRO-GENERICO
               END-IF
           END-IF
           MOVE  DCLPDD-VALOR-TOTAL        TO VALPAGO

           PERFORM 999-TRATA-FASE2
           .
       200-FASE2.
           EXEC CICS HANDLE AID
              PF1     (210-PF1)
              PF2     (220-PF2)
              PF10    (260-PF10)
              PF11    (280-PF11)
              PF12    (290-PF12)
              ENTER   (290-ENTER)
              ANYKEY  (240-ANYKEY)
           END-EXEC
      *
           EXEC CICS RECEIVE
                MAP     ('MAPAPAG')
                MAPSET  ('T10MPAG')
                INTO    (MAPAPAGI)
           END-EXEC
           .
       210-PF1.
           IF ( DEBPAGL  <= 0 ) AND ( CREPAGL <= 0) OR
              ( DEBPAGL  >  0 ) AND ( CREPAGL >  0)
               MOVE 'DEVE SER PREENCHIDO UM DOS CAMPOS'
                                          TO MENPAGO
              PERFORM 999-TRATA-FASE2
           END-IF
           IF (DEBPAGI NOT EQUAL 'X') AND (CREPAGI NOT EQUAL 'X')
              MOVE 'DEVE SER DIGITADO X NO DEBITO OU CREDITO'
                                       TO MENPAGO
              PERFORM 999-TRATA-FASE2
           END-IF
           IF DEBPAGL > 0
              MOVE 'D'               TO DCLCRT-TIPO
           ELSE
              IF  CREPAGL > 0
                  MOVE 'C'               TO DCLCRT-TIPO
              END-IF
           END-IF
           MOVE WS-ID-PEDIDO               TO DCLPDD-ID-PEDIDO

           MOVE DCLCRT-TIPO        TO WS-TIPO
           PERFORM 999-CHAMA-PROXIMO-PROGRAMA
           .
       220-PF2.
           MOVE '1'                        TO WS-FASE
      *
           EXEC CICS XCTL
               PROGRAM('T08PENT')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
      *
           .
       260-PF10.
           MOVE '1'                        TO WS-FASE
      *
           EXEC CICS XCTL
               PROGRAM('T11PCAR')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
      *
           .
       280-PF11.
           MOVE '1'                        TO WS-FASE
      *
           EXEC CICS XCTL
               PROGRAM('T11PPED')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
      *
           .
       290-PF12.
           MOVE '1'                        TO WS-FASE
      *
           EXEC CICS XCTL
               PROGRAM('T11PLOG')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
      *
           .
       240-ANYKEY.
           MOVE 'TECLA PRESSIONADA INVALIDA'
                                           TO MENPAGO
           PERFORM 999-TRATA-FASE2
           .
       290-ENTER.
           MOVE 'TECLA PRESSIONADA INVALIDA'
                                           TO MENPAGO
           PERFORM 999-TRATA-FASE2
           .
       999-CHAMA-PROXIMO-PROGRAMA.
           MOVE '1'           TO WS-FASE
           EXEC CICS XCTL
               PROGRAM('T10PCDA')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
       999-ENCERRA-TRANSACAO.
           MOVE +80                TO WS-LENGTH
           EXEC CICS SEND TEXT
               FROM (WS-MSG-ERRO)
               LENGTH(WS-LENGTH)
               ERASE FREEKB ALARM
           END-EXEC
      *
           EXEC CICS RETURN
           END-EXEC
           .
       999-MANDA-TELA.
           MOVE EIBTRMID                    TO TERPAGO
           MOVE EIBTRNID                    TO TRAPAGO
           MOVE EIBTASKN                    TO TASPAGO
           MOVE WS-FASE                     TO FASPAGO
      *
      *
           EXEC CICS LINK
              PROGRAM('AUXCICS1')
              COMMAREA(WS-VAR-TEMPO)
              LENGTH(+15)
           END-EXEC
           MOVE WS-DATA                    TO DATPAGO
           MOVE WS-HORARIO                 TO HORPAGO
      *
           EXEC CICS SEND
              MAP      ('MAPAPAG')
              MAPSET   ('T10MPAG')
              FROM     (MAPAPAGO)
              ERASE  FREEKB ALARM CURSOR
           END-EXEC
           .
       999-CHAMA-FASE1.
           MOVE '1'                        TO WS-FASE
      *
           EXEC CICS XCTL
               PROGRAM('T11PPAG')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
      *
           .
       999-CHAMA-FASE2.
           MOVE '2'                        TO WS-FASE
      *
           EXEC CICS RETURN
               TRANSID('FT0H')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH (LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
       999-TRATA-FASE2.
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .
       999-MAPFAIL.
           MOVE 'ERRO MAPA T11MPAG'       TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
       999-ERRO-GENERICO.
           MOVE SQLCODE             TO WS-SQLCODE
           STRING  'ERRO GENERICO' DELIMITED BY SPACES
                   'SQLC:' WS-SQLCODE DELIMITED BY SIZE

                      INTO WS-MSG-ERRO
      *    MOVE 'ERRO GENERICO'          TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
       999-NOTFND.
           MOVE 'NAO EXISTEM PEDIDOS  COM CODIGO MAIOR OU IGUAL AO INFOR
      -         'MADO'                     TO MENPAGO
           PERFORM 999-TRATA-FASE2
           .

