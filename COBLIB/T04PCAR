      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         T04PCAR.
       AUTHOR.                             GABRIEL/GUILHERME.

      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
       77  WS-MSG-ERRO                     PIC X(80).
       77  WS-LENGTH                       PIC S9(04) COMP.

      *----------------------------------------------------------------*
      *    IMPRESSAO DO MAPA                           *
      *----------------------------------------------------------------*
       01  LD-LINDET.
           05 FILLER                       PIC X(01) VALUE SPACES.

           05 LD-ID                        PIC ZZZZZ9.
           05 FILLER                       PIC X(02) VALUE SPACES.

           05 LD-PRODUTO                   PIC X(25).
           05 FILLER                       PIC X(02) VALUE SPACES.

           05 LD-VNDDPOR                   PIC X(20).
           05 FILLER                       PIC X(02) VALUE SPACES. 

           05 LD-QTD                       PIC ZZZ9.
           05 FILLER                       PIC X(02) VALUE SPACES.

           05 FILLER                       PIC X(02) VALUE 'R$'.
           05 LD-VALOR                     PIC Z.ZZZ.ZZ9,99.
           05 FILLER                       PIC X(01) VALUE SPACES.

      *----------------------------------------------------------------*
      *    VARIAVEIS DE DATA
      *----------------------------------------------------------------*

       01  WS-DATA.
           05 WS-ANO                       PIC X(02).
           05 WS-MES                       PIC X(02).
           05 WS-DIA                       PIC X(02).

       01  WS-HORARIO.
           05 WS-HORA                      PIC X(02).
           05 WS-MIN                       PIC X(02).
           05 WS-SEG                       PIC X(02).

       01  WS-DATA-F.
           05 WS-ANO-F                     PIC X(02).
           05 FILLER                       PIC X(01) VALUE '/'.
           05 WS-MES-F                     PIC X(02).
           05 FILLER                       PIC X(01) VALUE '/'.
           05 WS-DIA-F                     PIC X(02).

       01  WS-HORARIO-F.
           05 WS-HORA-F                    PIC X(02).
           05 FILLER                       PIC X(01) VALUE ':'.
           05 WS-MIN-F                     PIC X(02).
           05 FILLER                       PIC X(01) VALUE ':'.
           05 WS-SEG-F                     PIC X(02).

      *----------------------------------------------------------------*
      *    DFHCOMMAREA E FILA
      *----------------------------------------------------------------*

       01  WS-DFHCOMMAREA.
           05 WS-FASE                      PIC X(01).
           05 WS-CPF-DFHCOMMAREA           PIC X(11).
           05 WS-ID-PRODUTO                PIC 9(09).
           05 WS-CONT-CARR                 PIC 9(09).
           05 WS-ID-PEDIDO                 PIC 9(09).

       77  WS-PAGA                         PIC 9(02).
       77  WS-PAGT                         PIC 9(02).
                      
       77  WS-EIBRESP                      PIC X(30).
       77  WS-SQLCODE                      PIC +9(9).
       77  WS-FILE-NAME                    PIC X(08)
                                                   VALUE 'T04MPRLT'.
       77  WS-QUEUE-NAME                   PIC X(08)
                                                   VALUE 'QT3PCAR'.
       77  WS-ITEM                         PIC S9(04) COMP.
       77  WS-RESP                         PIC S9(08) COMP.
       77  WS-RESTO                        PIC 9(01).
       77  WS-INDEX                        PIC 9(02).

       01  WS-ENDFILE-AUX                  PIC X(01).
           88 ENDFILE-YES                          VALUE 'S'.
           88 ENDFILE-NO                           VALUE 'N'.

      *----------------------------------------------------------------*
      *    AUXILIARES
      *----------------------------------------------------------------*
       01  WS-AUXILIARES.
           05  WS-PRODUTO                   PIC X(24).
           05  WS-PRODUTO-AUX               PIC X(24).
           05  WS-PRODUTO-MAIN              PIC X(25).
           05  WS-VENDIDO-POR               PIC X(19).
           05  WS-VENDIDO-POR-AUX           PIC X(19).
           05  WS-VENDIDO-MAIN              PIC X(20).
           05  WS-ID                        PIC S9(9) COMP.
           05  WS-VALOR-MINIMO              PIC S9(7) USAGE COMP-3.
           05  WS-VALOR-MAXIMO              PIC S9(7) USAGE COMP-3.

       01  WS-DFHCOMMAREA-AUX.
           05 WS-FASE-AUX                   PIC X(01).
           05 WS-ID-CPF-AUX                 PIC X(11).
           05 WS-ID-PROD-AUX                PIC 9(09).
           05 WS-CONT-CARR-AUX              PIC 9(09).
           05 WS-ID-PEDIDO-AUX              PIC 9(09).

           COPY T04MCAR.

           COPY DFHAID.
           COPY DFHBMSCA.

           EXEC SQL
              INCLUDE TCDCLPDD
           END-EXEC.
             EXEC SQL
               INCLUDE TCDCLPRP
             END-EXEC.
             EXEC SQL
               INCLUDE TCDCLCAR
             END-EXEC.

           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.

      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
       01  DFHCOMMAREA.
           05 OCCURS 0 TO 24576 TIMES DEPENDING ON EIBCALEN
                                           PIC X(01).
      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
           EXEC CICS HANDLE CONDITION
              MAPFAIL (999-MAPFAIL)
              ERROR   (999-ERROR  )
           END-EXEC

      * SELETOR DE FASE - A CONSULTA POSSUI 4 FASES
      *    FASE 1 - ENVIA O MAPA PARA O TERMINAL
      *    FASE 2 - TRATA O CAMPO T2COD (CHAVE)
           MOVE DFHCOMMAREA                TO WS-DFHCOMMAREA

           IF EIBCALEN = 0
              MOVE '1'                     TO WS-FASE
           END-IF

           MOVE DFHCOMMAREA                TO WS-DFHCOMMAREA

           EVALUATE WS-FASE
              WHEN '1' PERFORM 100-FASE1
              WHEN '2' PERFORM 200-FASE2
              WHEN OTHER
                 MOVE +80                  TO WS-LENGTH
                 MOVE 'ERRO NO NUMERO DA FASE'
                                           TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-EVALUATE
           .

        100-FASE1.
           MOVE '1'                        TO WS-FASE

           MOVE WS-ID-PEDIDO               TO DCLCAR-ID-PEDIDO

           EXEC SQL
               DECLARE LISTADB2 CURSOR FOR
               SELECT PR.NOME_PRODUTO, CR.QTD_PRODUTO, 
                      CR.VALOR_UNITARIO_PEDIDO, PR.VENDIDO_POR,
                      PR.ID_PRODUTO
               FROM PEDIDOS PD
               JOIN CARRINHO CR ON PD.ID_PEDIDO = CR.ID_PEDIDO
               JOIN PRODUTOS PR ON CR.ID_PRODUTO = PR.ID_PRODUTO
               WHERE PD.ID_PEDIDO = :DCLCAR-ID-PEDIDO
           END-EXEC

           PERFORM 999-CHAMA-CURSOR
           .

      *----------------------------------------------------------------*
      *    SETOR FASE2, BINDS DE COMANDO                               *
      *----------------------------------------------------------------*

        200-FASE2.
           MOVE '2'                        TO WS-FASE
           EXEC CICS HANDLE AID
               ENTER   (210-ENTER)
               PF1     (210-PF1)
               PF3     (230-PF3)
               PF7     (270-PF7)
               PF8     (280-PF8)
               PF12    (212-PF12)
              ANYKEY   (240-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPCAR')
              MAPSET('T04MCAR')
              INTO  (MAPCARO)
           END-EXEC
           .

        210-ENTER.
      *    MOVE WS-DFHCOMMAREA             TO WS-DFHCOMMAREA-AUX
      *    MOVE '1'                        TO WS-FASE-AUX
      *    EXEC CICS XCTL
      *        PROGRAM('T04PETG')
      *        COMMAREA(WS-DFHCOMMAREA-AUX)
      *        LENGTH(LENGTH OF WS-DFHCOMMAREA-AUX)
      *    END-EXEC

           MOVE 'FOI PRO PROGRAMA DE ENTREGA'
                                           TO T3MSGO
           PERFORM 999-TRATA-FASE2
           .

        210-PF1.
           IF EIBCPOSN <= 400 OR EIBCPOSN >= 1440
              MOVE 'POSICAO INVALIDA DO CURSOR'
                                           TO T3MSGO
             PERFORM 999-TRATA-FASE2
           END-IF

           COMPUTE WS-INDEX = EIBCPOSN / 80 - 6
           MOVE T3LINHAI(WS-INDEX)         TO LD-LINDET

           IF LD-PRODUTO EQUAL SPACES
              MOVE 'POSICAO NAO CONTEM PRODUTO'
                                           TO T3MSGO
              PERFORM 999-TRATA-FASE2
           END-IF

           MOVE LD-PRODUTO                TO WS-PRODUTO
           MOVE LD-ID                     TO WS-ID
           MOVE WS-ID-PEDIDO              TO DCLCAR-ID-PEDIDO

           EXEC SQL
             DELETE FROM CARRINHO 
               WHERE ID_PEDIDO = :DCLCAR-ID-PEDIDO
                 AND ID_PRODUTO = :WS-ID
           END-EXEC

           EVALUATE SQLCODE
               WHEN 0
                    STRING 'PRODUTO' DELIMITED BY SIZE 
                    WS-PRODUTO  DELIMITED BY SPACE
                    'REMOVIDO DO CARRINHO' DELIMITED BY SIZE
                                           INTO T3MSGO
                    PERFORM 999-CHAMA-CURSOR
               WHEN +100
                    MOVE 'PRODUTO NAO ENCONTRADO NO CARRINHO'
                                           TO T3MSGO
                    PERFORM 999-TRATA-FASE2
               WHEN OTHER
                    MOVE 'ERRO AO REMOVER PRODUTO DO CARRINHO'
                                           TO T3MSGO
                    PERFORM 999-ERROR
           END-EVALUATE
           PERFORM 999-TRATA-FASE2
           .

        230-PF3.
           MOVE '1'                       TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('T04PPRL')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

        240-ANYKEY.
           MOVE 'TECLA PRESSIONADA INVALIDA!'
                                           TO T3MSGO
           PERFORM 999-CARREGA-DETALHES
           PERFORM 999-TRATA-FASE2
           .

        270-PF7.
           IF WS-PAGA = 1
              MOVE 'TETO DA PAGINA ANTIGIDA'
                                          TO T3MSGO
              PERFORM 999-CARREGA-DETALHES
           ELSE
              SUBTRACT 1 FROM WS-PAGA
              PERFORM 999-CARREGA-DETALHES
           END-IF

           PERFORM 999-TRATA-FASE2
           .

        280-PF8.
           IF WS-PAGA = WS-PAGT
              MOVE 'ULTIMA PAGINA ANTIGIDA'
                                          TO T3MSGO
              PERFORM 999-CARREGA-DETALHES
           ELSE
              ADD 1 TO WS-PAGA
              PERFORM 999-CARREGA-DETALHES
           END-IF

           PERFORM 999-TRATA-FASE2
           .

        212-PF12.
           MOVE '1'                          TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('T04PLOG')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC.

      *----------------------------------------------------------------*
      *    SETOR SQL                                                   *
      *----------------------------------------------------------------*

        999-CARREGA-TS.
           EXEC SQL
               FETCH LISTADB2 INTO
                    :DCLPRD-NOME-PRODUTO,
                    :DCLCAR-QTD-PRODUTO,
                    :DCLCAR-VALOR-UNITARIO-PEDIDO,
                    :DCLPRD-VENDIDO-POR,
                    :DCLPRD-ID-PRODUTO
           END-EXEC

           EVALUATE SQLCODE
               WHEN 0
                    ADD 1                       TO WS-ITEM
                    MOVE DCLPRD-ID-PRODUTO      TO LD-ID 
                    MOVE DCLPRD-NOME-PRODUTO    TO LD-PRODUTO
                    MOVE DCLPRD-VENDIDO-POR     TO LD-VNDDPOR
                    MOVE DCLCAR-VALOR-UNITARIO-PEDIDO
                                                TO LD-VALOR
                    MOVE DCLCAR-QTD-PRODUTO     TO LD-QTD

                  EXEC CICS WRITEQ TS
                     QUEUE(WS-QUEUE-NAME)
                     FROM(LD-LINDET)
                     ITEM(WS-ITEM)
                     RESP(WS-RESP)
                  END-EXEC

                  IF WS-RESP NOT = DFHRESP(NORMAL)
                     PERFORM 999-ERROR
                  END-IF

               WHEN +100
                IF WS-ITEM = 0
                   MOVE 'CARRINHO VAZIO'     TO T3MSGO
                   PERFORM 999-TRATA-FASE2
                ELSE
                   SET ENDFILE-YES TO TRUE
                END-IF
               WHEN OTHER
                    MOVE 'ERRO AO CARREGAR CARRINHO'
                                           TO WS-MSG-ERRO
                    PERFORM 999-ERROR
            .

        999-CHAMA-CURSOR.
           EXEC CICS DELETEQ TS
              QUEUE(WS-QUEUE-NAME)
              RESP(WS-RESP)
           END-EXEC

           IF WS-RESP = DFHRESP(NORMAL) OR DFHRESP(QIDERR)
              CONTINUE
           ELSE
              PERFORM 999-ERROR
           END-IF

           EXEC SQL
               OPEN LISTADB2
           END-EXEC

           MOVE ZERO                       TO WS-ITEM
           SET ENDFILE-NO TO TRUE
           PERFORM 999-CARREGA-TS UNTIL ENDFILE-YES

           EXEC SQL
              CLOSE LISTADB2
           END-EXEC

           MOVE 1                          TO WS-PAGA

           DIVIDE WS-ITEM BY 12 GIVING WS-PAGT REMAINDER WS-RESTO

           IF WS-RESTO > 0
              ADD 1 TO WS-PAGT
           END-IF

           PERFORM 999-CARREGA-DETALHES
           PERFORM 999-TRATA-FASE2
           .

        999-CARREGA-DETALHES.
           COMPUTE WS-ITEM = (WS-PAGA - 1) * 10 + 1
           MOVE 0                          TO WS-INDEX

           PERFORM WITH TEST AFTER UNTIL WS-INDEX = 10
              EXEC CICS READQ TS
                 QUEUE (WS-QUEUE-NAME)
                 INTO  (LD-LINDET)
                 ITEM  (WS-ITEM)
                 RESP  (WS-RESP)
              END-EXEC

              IF WS-RESP = DFHRESP(NORMAL)
                 ADD 1 TO WS-INDEX
                 MOVE LD-LINDET            TO T3LINHAO(WS-INDEX)
              ELSE
                 IF WS-RESP = DFHRESP(ITEMERR)
                    PERFORM WITH TEST AFTER UNTIL WS-INDEX = 10
                       ADD 1 TO WS-INDEX
                       MOVE SPACES         TO T3LINHAO(WS-INDEX)
                    END-PERFORM
                 ELSE
                    PERFORM 999-ERROR
                 END-IF
              END-IF
              ADD 1 TO WS-ITEM
           END-PERFORM
           .

      *----------------------------------------------------------------*
      *    SETOR DE TELAS CICS                                         *
      *----------------------------------------------------------------*

        999-MANDA-TELA.
           MOVE EIBTRMID                  TO T3TERMO
           MOVE EIBTRNID                  TO T3TRANO
           MOVE EIBTASKN                  TO T3TASKO
           MOVE WS-FASE                   TO T3FASEO

           ACCEPT WS-DATA    FROM DATE
           ACCEPT WS-HORARIO FROM TIME

           MOVE WS-DIA     TO WS-DIA-F
           MOVE WS-MES     TO WS-MES-F
           MOVE WS-ANO     TO WS-ANO-F

           MOVE WS-HORA    TO WS-HORA-F
           MOVE WS-MIN     TO WS-MIN-F
           MOVE WS-SEG     TO WS-SEG-F

           MOVE WS-DATA-F                 TO T3DATAO
           MOVE WS-HORARIO-F              TO T3HORAO

           MOVE WS-PAGA                   TO T3PAGAO
           MOVE WS-PAGT                   TO T3PAGTO

           EXEC CICS SEND
               MAP ('MAPCAR')
               MAPSET('T04MCAR')
               FROM(MAPCARO)
               ERASE FREEKB ALARM CURSOR
           END-EXEC
           .

        999-CHAMA-FASE1.
           MOVE '1'                        TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('T04PPRL')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

        999-CHAMA-FASE2.
           MOVE '2'                        TO WS-FASE

           EXEC CICS RETURN
               TRANSID('FT4E')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
           
        999-TRATA-FASE2.
      *    MOVE -1 TO PRDL
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .

        999-ENCERRA-TRANSACAO.
           EXEC CICS SEND TEXT
               FROM (WS-MSG-ERRO)
               LENGTH(WS-LENGTH)
               ERASE FREEKB ALARM
           END-EXEC

           EXEC CICS RETURN
           END-EXEC
           .

        999-MAPFAIL.
           MOVE +80                        TO WS-LENGTH
           MOVE 'ERRO MAPA'                TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .

        999-ERROR.
           MOVE +80                        TO WS-LENGTH
           STRING 'CODIGO DO ERRO '        DELIMITED BY SIZE
                  WS-EIBRESP               DELIMITED BY SPACES
                  ' SQLCODE: '             DELIMITED BY SIZE
                  WS-SQLCODE               DELIMITED BY SIZE
                  'PRODUTO:  '             DELIMITED BY SIZE
                  WS-PRODUTO               DELIMITED BY SIZE
               INTO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
