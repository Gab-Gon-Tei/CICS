      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         T04PDTL.
       AUTHOR.                             GABRIEL

      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
       77  WS-MSG-ERRO                     PIC X(80).
       77  WS-LENGTH                       PIC S9(04) COMP.
       77  WS-CPF-ERRO                      PIC X(11).

       01  WS-DATA.
           05 WS-ANO                       PIC X(02).
           05 WS-MES                       PIC X(02).
           05 WS-DIA                       PIC X(02).

       01  WS-HORARIO.
           05 WS-HORA                      PIC X(02).
           05 WS-MIN                       PIC X(02).
           05 WS-SEG                       PIC X(02).

       01  WS-DATA-F.
           05 WS-DIA-F                     PIC X(02).
           05 FILLER                       PIC X(01) VALUE '/'.
           05 WS-MES-F                     PIC X(02).
           05 FILLER                       PIC X(01) VALUE '/'.
           05 WS-ANO-F                     PIC X(02).

       01  WS-HORARIO-F.
           05 WS-HORA-F                    PIC X(02).
           05 FILLER                       PIC X(01) VALUE ':'.
           05 WS-MIN-F                     PIC X(02).
           05 FILLER                       PIC X(01) VALUE ':'.
           05 WS-SEG-F                     PIC X(02).

       01  WS-DFHCOMMAREA.
           05 WS-FASE                      PIC X(01).
           05 WS-ID-PROD                   PIC S9(09) COMP.
           05 WS-ID-CPF                    PIC X(11).

      *MAPA REFERENTE A TELA DE CADASTRO
           COPY T04MDTL.
      *COMANDO TECLAS PRESSIONADAS
           COPY DFHAID.
      *CARACTERES E ATRIBUTOS
           COPY DFHBMSCA.
      *BOOK DAS VARIAVEIS HOSPEDEIRAS
           EXEC SQL
           INCLUDE BKPROD
           END-EXEC.
           EXEC SQL
           INCLUDE BKPEXPR
           END-EXEC.
           

      *BOOK DE VARIAVEIS DE USO DO SQL
           EXEC SQL
           INCLUDE SQLCA
           END-EXEC.
      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
       01  DFHCOMMAREA.
           05 OCCURS 0 TO 24579 TIMES DEPENDING ON EIBCALEN
                                           PIC X(01).
      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
      *ROTINA DEFINIDA PARA LIDAR COM ERROS DE MAPA E GENERICOS--------*
           EXEC CICS HANDLE CONDITION
              MAPFAIL(999-MAPFAIL)
              ERROR(999-ERROR)
           END-EXEC

           MOVE DFHCOMMAREA                TO WS-DFHCOMMAREA

           IF EIBCALEN EQUAL 0
              MOVE +80                     TO WS-LENGTH
              MOVE 'FACA O LOGIN NO SISTEMA PELA TRANSACAO FT4A'
                                           TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-IF

           EVALUATE WS-FASE
              WHEN '1' PERFORM 100-FASE1
              WHEN '2' PERFORM 200-FASE2

              WHEN OTHER
                 MOVE +80                  TO WS-LENGTH
                 MOVE 'ERRO NO NUMERO DA FASE'
                                           TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-EVALUATE
           .

       100-FASE1.
      *    MOVE LOW-VALUES                 TO MAPDTLO
           MOVE -1                         TO T1QTDL

           PERFORM 210-ENTER
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .

       200-FASE2.
           EXEC CICS HANDLE AID
              ENTER   (210-ENTER)
              PF3     (220-PF3)
              PF5     (230-PF5)
              CLEAR   (230-PF5)
              PF12    (240-PF12)
              ANYKEY  (250-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPDTL')
              MAPSET('T04MDTL')
              INTO  (MAPDTLI)
           END-EXEC
           .

       220-PF3.
           MOVE '1'                       TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('T04PPRL')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       230-PF5.
           PERFORM 999-CHAMA-FASE1
           .

       240-PF12.
           MOVE '1'                       TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('T04PLOG')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       250-ANYKEY.
           MOVE 'TECLA PRESSIONADA INVALIDA!'
                                           TO T1MSGO
           PERFORM 999-TRATA-FASE2
           .

       210-ENTER.
               MOVE WS-ID-PROD             TO DCLPROD-ID-PRODUTO
               MOVE WS-ID-CPF             TO T1MSGO

               EXEC SQL
                  SELECT
                     DESCRICAO
                    ,NOME_PRODUTO
                    ,QUANTIDADE_ESTOQUE
                    ,VALOR_UNITARIO
                  INTO
                    :DCLPROD-DESCRICAO
                   ,:DCLPROD-NOME-PRODUTO
                   ,:DCLPROD-QUANTIDADE-ESTOQUE
                   ,:DCLPROD-VALOR-UNITARIO
                  FROM PRODUTOS 
                  WHERE ID_PRODUTO = :DCLPROD-ID-PRODUTO
 
               END-EXEC

               EVALUATE SQLCODE
                 WHEN 0
                   MOVE DCLPROD-NOME-PRODUTO-TEXT TO T1PRODO
      *            MOVE DCLPROD-NOME-PRODUTO TO T1MSGO
                   MOVE DCLPROD-DESCRICAO    TO T1DESCO
                   MOVE DCLPROD-QUANTIDADE-ESTOQUE TO T1ESTO
                   MOVE DCLPROD-VALOR-UNITARIO TO T1VALO
                 WHEN -545
                    MOVE ' SQLCODE ERRO -545'
                                           TO T1MSGO
                    PERFORM 999-TRATA-FASE2
                 WHEN OTHER
                    PERFORM 999-ERROR
               END-EVALUATE        
           IF WS-FASE EQUAL '1'
               PERFORM 999-TRATA-FASE2
           ELSE
               PERFORM 215-INCLUIR
               PERFORM 220-PF3
           END-IF
           .
       215-INCLUIR.
           MOVE T1QTDI                  TO DCLPEPR-QTD-PRODUTO
           COMPUTE DCLPEPR-VALOR-UNITARIO-PEDIDO
               = DCLPROD-VALOR-UNITARIO * DCLPEPR-QTD-PRODUTO
           MOVE DCLPROD-ID-PRODUTO      TO DCLPEPR-ID-PRODUTO
           MOVE WS-ID-CPF               TO DCLPEPR-CPF
           EXEC SQL
               INSERT INTO PEDIDOS_PRODUTOS
                 (ID_PRODUTO
                 ,QTD_PRODUTO
                 ,VALOR_UNITARIO_PEDIDO
                 ,CPF)
                VALUES
                (:DCLPEPR-ID-PRODUTO
                 ,:DCLPEPR-QTD-PRODUTO
                 ,:DCLPEPR-VALOR-UNITARIO-PEDIDO
                 ,:DCLPEPR-CPF)
           END-EXEC.
           EVALUATE SQLCODE
               WHEN 0
                   MOVE 'INCLUSAO EFETUADA COM SUCESSO'
                                           TO T1MSGO
               WHEN -803
                   MOVE 'ERRO - PEDIDO JA EXISTE'
                                           TO T1MSGO
                   PERFORM 999-TRATA-FASE2
               WHEN OTHER
                   PERFORM 999-ERROR
           END-EVALUATE.

       999-ENCERRA-TRANSACAO.
           EXEC CICS SEND TEXT
              FROM (WS-MSG-ERRO)
              LENGTH(WS-LENGTH)
              ERASE FREEKB ALARM
           END-EXEC

           EXEC CICS RETURN
           END-EXEC
           .

       999-MANDA-TELA.
           MOVE EIBTRMID                  TO T1TERMO
           MOVE EIBTRNID                  TO T1TRANSO
           MOVE EIBTASKN                  TO T1TASKO
           MOVE WS-FASE                   TO T1FASEO

           ACCEPT WS-DATA FROM DATE
           ACCEPT WS-HORARIO FROM TIME

           MOVE WS-DIA                     TO WS-DIA-F
           MOVE WS-MES                     TO WS-MES-F
           MOVE WS-ANO                     TO WS-ANO-F

           MOVE WS-HORA                    TO WS-HORA-F
           MOVE WS-MIN                     TO WS-MIN-F
           MOVE WS-SEG                     TO WS-SEG-F

           MOVE WS-DATA-F                   TO T1DATAO
           MOVE WS-HORARIO-F                TO T1HORAO

           EXEC CICS SEND
              MAP ('MAPDTL')
              MAPSET('T04MDTL')
              FROM(MAPDTLO)
              ERASE FREEKB ALARM CURSOR
           END-EXEC
           .

       999-CHAMA-FASE1.
           MOVE '1'                       TO WS-FASE

           EXEC CICS XCTL
              PROGRAM('T04PDTL')
              COMMAREA(WS-DFHCOMMAREA)
              LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-CHAMA-FASE2.
           MOVE '2'                       TO WS-FASE
           EXEC CICS RETURN
               TRANSID('FT4D')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-TRATA-FASE2.
           MOVE -1                        TO T1QTDL

           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .

       999-MAPFAIL.
           MOVE 'ERRO MAPA T04MCAD'        TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .

       999-ERROR.
           MOVE 'ERRO GENERICO T04PCAD'   TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
